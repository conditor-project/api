{
    function buildRange(from, to){
        const range = {};
        if(from){ range.from = from; }
        if(to){ range.to = to; }
        return range;
    };
}

Start
  = Aggregation+ // First level exception, no need round brackets
  / Aggregations
  / _ { return []; }

Aggregations
  = agg:Aggregation { return [agg]; }
  / _ "(" _ aggs:Aggregation* _ ")" _ { return aggs; }

Aggregation
  = aggs:AggregationType _ subAggs:SubAggregations? _ { if(subAggs){ aggs.aggs = subAggs; } return aggs; }

SubAggregations
  = ">" _ aggs:Aggregations { return aggs; }

AggregationType
  = TermsAgg
  / DateRangeAgg
  / CardinalityAgg
  / NestedAgg
  / GlobalAgg

GlobalAgg
  = _ type:"global"i _ options:AggOptions? { return Object.assign({}, options, {type:"global"});}

NestedAgg
  = _ type:"nested"i _ ":" _ path:NestedField _ { return {path, type:"nested"}; }

CardinalityAgg
  = _ type:"cardinality"i _ ":" _ field:AggField _ options:AggOptions?
   { return Object.assign({}, options, {field, type:"cardinality"}); }

TermsAgg
  = _ type:"terms"i _ ":" _ field:AggField _ options:AggOptions?
   { return Object.assign({}, options, {field, type:"terms"}); }

DateRangeAgg
  = _ type:"date_range"i _ ":" _ field:AggField _ ranges:Ranges _ options:AggOptions?
   { return Object.assign({}, options, {type:"date_range", field, ranges}); }

Ranges
  = ":" _ range:Range* { return range; }

Range
  =  _ "[" _ from:From? _ to:To? _ "]" _ { return buildRange(from, to); }

From
  = DateMath

To
  = "TO"i _ dateMath:DateMath { return dateMath; }

DateMath
  = $(Date ("||" DateMathExpr)?)
  / (NowFlag dateMathExpr:DateMathExpr? {return "now" +  dateMathExpr})

Date
  = (
      StrictDateHourMinuteSecond
      / StrictDate
      / StrictYear !Digit
      / TimeStamp
    ) { return text(); }

TimeStamp
  = $(Minus? Digit+)

NowFlag
  = "now"i

DateMathExpr
  = $([0-9+\-yMwdhHms\/]*)


StrictDateHourMinuteSecond
  = StrictDate (" "/"T"i) StrictHourMinuteSecond

StrictHourMinuteSecond
  = $([0-1] [0-9] / "2" [0-3]) ":" $([0-5] [0-9]) ":" $([0-5] [0-9])

StrictDate
  = StrictYear "-" StrictMonth "-" StrictDay

StrictYear
  = $(Minus? Digit Digit Digit Digit)

StrictMonth
  = $("0" [1-9] / "1" [0-2])

StrictDay
  = $("0" [1-9] / [1-2] [0-9] / "3" [0-1])

NestedField
  = StringLiteral

AggField
  = StringLiteral

AggOptions
  =  ":"  options:Object { return options; }
