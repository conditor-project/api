{
    function buildRange(from, to){
        const range = {};
        if(from){ range.from = from; }
        if(to){ range.to = to; }
        return range;
    };
}
Start
  = Aggregations

Aggregations
  = agg:Aggregation { return [agg]; }
  / _ "(" _ aggs:Aggregation* _ ")" _ { return aggs; }

Aggregation
  = aggs:AggregationType _ subAggs:SubAggregations? { if(subAggs){aggs.aggs = subAggs}; return aggs; }

SubAggregations
  = ">" _ aggs:Aggregations { return aggs; }

AggregationType
  = TermsAgg
  / DateRangeAgg
  / CardinalityAgg

CardinalityAgg
  = _ type:"cardinality"i _ ":" _ field:AggField _ options:AggOptions?
   { return Object.assign({field, type:"cardinality"}, options); }

TermsAgg
  = _ type:"terms"i _ ":" _ field:AggField _ options:AggOptions?
   { return Object.assign({field, type:"terms"}, options); }

DateRangeAgg
  = _ type:"date_range"i _ ":" _ field:AggField _ ranges:Ranges _ options:AggOptions?
   { return Object.assign({type:"date_range", field, ranges}, options); }

Ranges
  = ":" _ range:Range* { return range; }

Range
  =  _ "[" _ from:From? _ to:To? _ "]" _ { return buildRange(from, to); }

From
  = DateMath

To
  = "to"i _ dateMath:DateMath { return dateMath; }

DateMath
  = $(Date ("||" DateMathExpr)?)
  / (NowFlag dateMathExpr:DateMathExpr? {return "now" +  dateMathExpr})

Date
  = (
      StrictDateHourMinuteSecond
      / StrictDate
      / StrictYear !Digit
      / TimeStamp
    ) { return text(); }

TimeStamp
  = $(Minus? Digit+)

NowFlag
  = "now"i

DateMathExpr
  = $([0-9+\-yMwdhHms\/]*)


StrictDateHourMinuteSecond
  = StrictDate (" "/"T"i) StrictHourMinuteSecond

StrictHourMinuteSecond
  = $([0-1] [0-9] / "2" [0-3]) ":" $([0-5] [0-9]) ":" $([0-5] [0-9])

StrictDate
  = StrictYear "-" StrictMonth "-" StrictDay

StrictYear
  = $(Minus? Digit Digit Digit Digit)

StrictMonth
  = $("0" [1-9] / "1" [0-2])

StrictDay
  = $("0" [1-9] / [1-2] [0-9] / "3" [0-1])

AggField
  = StringLiteral

AggOptions
  =  ":"  json:JsonText { return json; }

// JSON Grammar
// ============
//

// ----- 2. JSON Grammar -----

JsonText
  = _ Value:Value _ { return Value; }

BeginArray     = _ "[" _
BeginObject    = _ "{" _
EndArray       = _ "]" _
EndObject      = _ "}" _
NameSeparator  = _ ":" _
ValueSeparator = _ "," _

_
 = Whitespace*

Whitespace "Whitespace" = [ \t\n\r]

// ----- 3. Values -----

Value
  = False
  / Null
  / True
  / Object
  / Array
  / Number
  / StringLiteral

False = "false" { return false; }
Null  = "null"  { return null;  }
True  = "true"  { return true;  }

// ----- 4. Objects -----

Object
  = BeginObject
    members:(
      head:Member
      tail:(ValueSeparator m:Member { return m; })*
      {
        var result = {};

        [head].concat(tail).forEach(function(element) {
          result[element.name] = element.Value;
        });

        return result;
      }
    )?
    EndObject
    { return members !== null ? members: {}; }

Member
  = name:StringLiteral NameSeparator Value:Value {
      return { name: name, Value: Value };
    }

// ----- 5. Arrays -----

Array
  = BeginArray
    values:(
      head:Value
      tail:(ValueSeparator v:Value { return v; })*
      { return [head].concat(tail); }
    )?
    EndArray
    { return values !== null ? values : []; }

// ----- 6. Numbers -----

Number "number"
  = Minus? Int Frac? Exp? { return parseFloat(text()); }

Decimalpoint
  = "."

Digit1_9
  = [1-9]

E
  = [eE]

Exp
  = E (Minus / Plus)? Digit+

Frac
  = Decimalpoint Digit+

Int
  = Zero / (Digit1_9 Digit*)

Minus
  = "-"

Plus
  = "+"

Zero
  = "0"

// ----- 7. Strings -----

StringLiteral "string"
  = '"' chars:DoubleStringCharacter* '"' { return chars.join(""); }
  / "'" chars:SingleStringCharacter* "'" { return chars.join("") }
  /  chars:IdentifierCharacter* { return chars.join(""); }
  
DoubleStringCharacter
  = $Char

SingleStringCharacter
  = $(!("'") Char)

IdentifierCharacter
  = [A-Za-z0-9_\-.]
  
Char
  = Unescaped
  / Escape
    sequence:(
        '"'
      / "\\"
      / "/"
      / "b" { return "\b"; }
      / "f" { return "\f"; }
      / "n" { return "\n"; }
      / "r" { return "\r"; }
      / "t" { return "\t"; }
      / "u" digits:$(HexDigit HexDigit HexDigit HexDigit) {
          return String.fromCharCode(parseInt(digits, 16));
        }
    )
    { return sequence; }

Escape
  = "\\"

Unescaped
  = [^\0-\x1F\x22\x5C]

// ----- Core ABNF Rules -----

// See RFC 4234, Appendix B (http://tools.ietf.org/html/rfc4234).
Digit  = [0-9]
HexDigit = [0-9a-f]i

ReservedCharacters
  = "+"
  / "-"
  / "="
  / "&&"
  / "||"
  / ">"
  / "<"
  / "!"
  / "("
  / ")"
  / "{"
  / "}"
  / "["
  / "]"
  / "^"
  / '"'
  / "'"
  / "~"
  / "*"
  / "?"
  / ":"
  / "\\"
  / "/"
  / ","
